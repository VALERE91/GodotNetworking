import os

Import('env')

# Clone env so network settings don't bleed into gameplay
net_env = env.Clone()
net_env.Append(CPPPATH=["."]) 

sources = Glob("*.cpp")
# Output to game/bin/libnetwork...
lib_path = "../../game/bin/libnetwork" + net_env["suffix"]

library = net_env.SharedLibrary(target=lib_path, source=sources)
Default(library)

# --- AUTO-GENERATE .gdextension FILE ---
def generate_gdextension_file(target, source, env):
    # configuration
    output_dir = target[0].dir.get_abspath()
    file_name = "network.gdextension"
    entry_symbol = "gameplay_network_init"
    lib_name = "libnetwork"
    
    output_path = os.path.join(output_dir, file_name)
    
    content = f"""[configuration]
entry_symbol = "{entry_symbol}"
compatibility_minimum = "4.2"

[libraries]
macos.debug = "res://bin/{lib_name}.macos.template_debug.arm64.dylib"
macos.release = "res://bin/{lib_name}.macos.template_release.arm64.dylib"

windows.debug.x86_64 = "res://bin/{lib_name}.windows.template_debug.x86_64.dll"
windows.release.x86_64 = "res://bin/{lib_name}.windows.template_release.x86_64.dll"

linux.debug.x86_64 = "res://bin/{lib_name}.linux.template_debug.x86_64.so"
linux.release.x86_64 = "res://bin/{lib_name}.linux.template_release.x86_64.so"
"""
    
    os.makedirs(os.path.dirname(output_path), exist_ok=True)
    with open(output_path, "w") as f:
        f.write(content)
        print(f"Generated {file_name} at {output_path}")

net_env.AddPostAction(library, generate_gdextension_file)